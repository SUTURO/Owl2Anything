import com.github.jk1.license.render.InventoryHtmlReportRenderer
import com.github.jk1.license.filter.LicenseBundleNormalizer

plugins {
    id 'java'
    id 'java-library-distribution'
    id 'com.palantir.git-version' version '3.0.0'
    id 'com.github.jk1.dependency-license-report' version '2.0'
}

apply from: 'versions.gradle'

group 'com.malte3d.suturo'
version gitVersion()

sourceCompatibility = 17
targetCompatibility = 17

def mainClassName = 'com.malte3d.suturo.knowledge.owl2anything.Owl2Anything'
def outputDirName = 'owl2anything'

repositories {
    mavenCentral()
}

dependencies {

    implementation "net.sourceforge.owlapi:owlapi-api:${versions.owlapi}"
    implementation "net.sourceforge.owlapi:owlapi-apibinding:${versions.owlapi}"

    implementation "commons-cli:commons-cli:${versions.commons_cli}"
    implementation "org.apache.commons:commons-csv:${versions.commons_csv}"
    implementation "com.google.code.gson:gson:${versions.gson}"

    implementation "org.slf4j:slf4j-api:${versions.sl4j}"
    implementation "org.apache.logging.log4j:log4j-core:${versions.log4j}"
    implementation "org.apache.logging.log4j:log4j-slf4j2-impl:${versions.log4j}"

    compileOnly "org.projectlombok:lombok:${versions.lombok}"
    annotationProcessor "org.projectlombok:lombok:${versions.lombok}"

    testCompileOnly "org.projectlombok:lombok:${versions.lombok}"
    testAnnotationProcessor "org.projectlombok:lombok:${versions.lombok}"

    testImplementation "org.junit.jupiter:junit-jupiter-api:${versions.junit}"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${versions.junit}"
}

test {
    useJUnitPlatform()
}

jar {

    manifest {
        attributes 'Main-Class': mainClassName
        attributes 'Class-Path': configurations.runtimeClasspath.files.collect { "lib/$it.name" }.join(' ')
    }

    exclude 'META-INF/*.SF', 'META-INF/*.DSA', 'META-INF/*.RSA', 'META-INF/*.MF'
    from "LICENSE"
}

distributions {

    main {
        contents {

            from "${project.buildDir}/license-report"

            into(outputDirName) {
                from "${project.rootDir}/${outputDirName}/iri_mapping_default.csv"
                from "${project.rootDir}/${outputDirName}/iri_namespaces_default.csv"
            }
        }
    }
}

distZip.dependsOn generateLicenseReport

licenseReport {

    outputDir = "${project.buildDir}/license-report/licenses"

    projects = [project] + project.subprojects

    configurations = ['runtimeClasspath']

    excludeGroups = []
    excludes = []
    excludeOwnGroup = true
    excludeBoms = true

    filters = [new LicenseBundleNormalizer(bundlePath: "${project.rootDir}/dependency-license-bundle.json")]
    renderers = [new InventoryHtmlReportRenderer('dependency-license-report.html')]
}

clean {
    delete "${project.rootDir}/${outputDirName}/output"
}